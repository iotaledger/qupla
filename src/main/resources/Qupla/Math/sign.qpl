// LUT logic: return the sign of a 3-trit value with least significant trit first
//            return sign(trit1, trit2, trit3)
// note that the sign of a trit *is* the trit, so we start at Tryte instead
lut sign {
  -,-,- = -
  0,-,- = -
  1,-,- = -
  -,0,- = -
  0,0,- = -
  1,0,- = -
  -,1,- = -
  0,1,- = -
  1,1,- = -
  -,-,0 = -
  0,-,0 = -
  1,-,0 = -
  -,0,0 = -
  0,0,0 = 0
  1,0,0 = 1
  -,1,0 = 1
  0,1,0 = 1
  1,1,0 = 1
  -,-,1 = 1
  0,-,1 = 1
  1,-,1 = 1
  -,0,1 = 1
  0,0,1 = 1
  1,0,1 = 1
  -,1,1 = 1
  0,1,1 = 1
  1,1,1 = 1
}

// returns (val < 0) ? - : (val > 0) : 1 : 0

func Trit sign<Trit> (Trit val) {
  return val
}

template sign3<T> = A + B + C {
  type A [T / 3]
  type B [T / 3]
  type C [T / 3]

  func Trit sign<T> (T val) {
    val0 = sign<A>(val[0 : A])
    val1 = sign<B>(val[A : B])
    val2 = sign<C>(val[A + B : C])
    return sign[val0, val1, val2]
  }
}

template sign<T> {
  type A [T - T / 2]
  type B [T - A]

  func Trit sign<T> (T val) {
    val0 = sign<A>(val[0 : A])
    val1 = sign<B>(val[A : B])
    return sign[val0, val1, 0]
  }
}


test - = sign<Tiny>(-99)
test - = sign<Tiny>(-1)
test 0 = sign<Tiny>(0)
test 1 = sign<Tiny>(1)
test 1 = sign<Tiny>(99)

test - = sign<Tiny2>(-99)
test - = sign<Tiny2>(-1)
test 0 = sign<Tiny2>(0)
test 1 = sign<Tiny2>(1)
test 1 = sign<Tiny2>(99)
