import Qupla

// Fibonacci: 0, 1, 1, 2, 3, 5, 8, 13, 21, 34, 55, 89, ...

// returns the n-th Fibonacci number,
// shitty version due to 2^n function calls
// acts as if it hangs due to exponential calls for n=30
// Java equivalent:
// return (n < 2) ? n : (fib(n - 1) + fib(n - 2))

type FibInt [Huge]

func FibInt fibonacci (FibInt n) {
  // check if n < 2, which means return n, otherwise recurse
  retN = isLess[cmp<FibInt>(n, 2)]

  // recursion case
  altN = nullify<FibInt>(not[retN], n)
  nMin1 = decr<FibInt>(altN)
  nMin2 = decr<FibInt>(nMin1)
  nextFib = add<FibInt>(fibonacci(nMin1), fibonacci(nMin2))

  return nullify<FibInt>(retN, n) | nextFib
}

// returns the n-th Fibonacci number,
// smart version, using n function calls
// can go up to n=180 when using a Huge
// Java equivalent:
// return (n == 0 || n == 1) ? n : fibPart2(n - 2, 0, 1)

func FibInt fibonacci2 (FibInt n) {
  // check if n equals 0 or 1, which means return n, otherwise recurse
  retFib = notLess[cmp<FibInt>(n, 2)]

  // recursion case
  altN = nullify<FibInt>(retFib, n)
  nMin2 = sub<FibInt>(altN, 2)
  fib0 = nullify<FibInt>(retFib, 0)
  fib1 = nullify<FibInt>(retFib, 1)

  return nullify<FibInt>(not[retFib], n) | fibPart2(nMin2, fib0, fib1)
}

// helper function
// Java equivalent:
// fib2 = fib0 + fib1
// return (n == 0) ? fib2 : fibPart2(n - 1, fib1, fib2)

func FibInt fibPart2 (FibInt n, FibInt fib0, FibInt fib1) {
  // check if n equals 0, which means return fib2, otherwise recurse
  retFib = notZero[sign<FibInt>(n)]

  fib2 = add<FibInt>(fib0, fib1)

  // recursion case
  altN = nullify<FibInt>(retFib, n)
  nMin1 = decr<FibInt>(altN)
  newFib0 = nullify<FibInt>(retFib, fib1)
  newFib1 = nullify<FibInt>(retFib, fib2)

  return nullify<FibInt>(not[retFib], fib2) | fibPart2(nMin1, newFib0, newFib1)
}

// test statements

test  0 = fibonacci(0)
test  1 = fibonacci(1)
test  1 = fibonacci(2)
test  2 = fibonacci(3)
test  3 = fibonacci(4)
test  5 = fibonacci(5)
test  8 = fibonacci(6)
test 55 = fibonacci(10)

test  0 = fibonacci2(0)
test  1 = fibonacci2(1)
test  1 = fibonacci2(2)
test  2 = fibonacci2(3)
test  3 = fibonacci2(4)
test  5 = fibonacci2(5)
test  8 = fibonacci2(6)
test 55 = fibonacci2(10)

eval fibonacci2(180)
